# Based on https://github.com/ChristopherBiscardi/advent-of-code/tree/main/2023/rust
create day:
    cargo generate --path ./template --name {{day}}
    just get-input {{day}}

work day:
    cargo watch --clear -w {{day}} -x "check -p {{day}}" -s "just test {{day}}" -s "just lint {{day}}"

test day:
    cargo nextest run -p {{day}}

lint day:
    cargo clippy -p {{day}}

profile day:
    cargo build --profile profiling -p {{day}}
    samply record ./target/profiling/{{day}}

# You can find SESSION by using Chrome tools:
# 1) Go to https://adventofcode.com/2022/day/1/input
# 2) right-click -> inspect -> click the "Application" tab.
# 3) Refresh
# 5) Click https://adventofcode.com under "Cookies"
# 6) Grab the value for session. Fill it into your .env file
# 
# example .env:
#
# ```
# SESSION=PASTE_COOKIE_VALUE_HERE
# ```
#
# get the input for a day's puzzle
get-input day:
    ./scripts/get-aoc-input.rs --day {{day}} --current-working-directory {{justfile_directory()}}
